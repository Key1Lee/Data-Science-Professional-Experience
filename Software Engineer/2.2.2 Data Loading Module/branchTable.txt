DROP TABLE CDW_SAPP_D_BRANCH;

CREATE EXTERNAL TABLE IF NOT EXISTS CDW_SAPP_BRANCH (
    BRANCH_CODE INT COMMENT 'Uniquely identifies a branch of the retail store',
	BRANCH_NAME VARCHAR(25) COMMENT 'Name of the Branch',
	BRANCH_STREET VARCHAR(30) COMMENT 'Street Address',
	BRANCH_CITY VARCHAR(30) COMMENT 'City name where the branch is located',
	BRANCH_STATE VARCHAR(30) COMMENT 'State name where the branch is located',
	BRANCH_ZIP INT COMMENT 'Zip postal code (default value 999999)',
	BRANCH_PHONE VARCHAR(13) COMMENT 'Phone number of the branch',
	LAST_UPDATED TIMESTAMP COMMENT 'Record inserted date.'
)
COMMENT 'Branch Staging Table'
ROW FORMAT DELIMITED FIELDS TERMINATED BY ',' 
LINES TERMINATED BY '\n'
STORED AS TEXTFILE
LOCATION '/user/maria_dev/Credit_Card_System/CDW_SAPP_BRANCH';

LOAD DATA INPATH '/user/maria_dev/Credit_Card_System/CDW_SAPP_BRANCH/' INTO TABLE CDW_SAPP_BRANCH;

CREATE EXTERNAL TABLE IF NOT EXISTS CDW_SAPP_D_BRANCH (
    BRANCH_CODE INT COMMENT 'Uniquely identifies a branch of the retail store',
	BRANCH_NAME VARCHAR(25) COMMENT 'Name of the Branch',
	BRANCH_STREET VARCHAR(30) COMMENT 'Street Address',
	BRANCH_CITY VARCHAR(30) COMMENT 'City name where the branch is located',
	BRANCH_ZIP INT COMMENT 'Zip postal code (default value 999999)',
	BRANCH_PHONE VARCHAR(13) COMMENT 'Phone number of the branch',
	LAST_UPDATED TIMESTAMP COMMENT 'Record inserted date.'
)
COMMENT 'Partitioned Branch Table'
PARTITIONED BY(BRANCH_STATE VARCHAR(30))
ROW FORMAT DELIMITED FIELDS TERMINATED BY ',' 
LINES TERMINATED BY '\n'
STORED AS TEXTFILE
LOCATION '/user/maria_dev/Credit_Card_System/CDW_SAPP_D_BRANCH';

SET hive.exec.dynamic.partition=true;
SET hive.exec.dynamic.partition.mode=nonstrict;

INSERT OVERWRITE TABLE CDW_SAPP_D_BRANCH
PARTITION (BRANCH_STATE)
SELECT BRANCH_CODE,
       BRANCH_NAME,
	   BRANCH_STREET,
	   BRANCH_CITY,
	   BRANCH_ZIP,
	   BRANCH_PHONE,
	   LAST_UPDATED,
	   BRANCH_STATE
FROM CDW_SAPP_BRANCH;

DROP TABLE CDW_SAPP_BRANCH;